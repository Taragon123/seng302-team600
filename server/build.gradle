plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	// Apply Jacoco test coverage reports
	id 'jacoco'
	// run Sonarqube from gradle task
	id "org.sonarqube" version "2.8"
}

group = 'com.spring-vue-gradle'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	cucumberRuntime {
		extendsFrom testImplementation
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.security:spring-security-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation group: 'org.mockito', name: 'mockito-core', version: '3.2.0'
	implementation 'org.mariadb.jdbc:mariadb-java-client:2.1.2'
	
	runtimeOnly 'com.h2database:h2'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'io.cucumber:cucumber-java:5.6.0'
	testImplementation 'io.cucumber:cucumber-junit:5.6.0'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

// Task used for just running cucumber tests, and can specify tags with -Ptags="@sometags"
task cucumber() {
	dependsOn assemble, compileTestJava
	String selectedTags = findProperty("tags") ?: ""

	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
					'--plugin', 'pretty', // How to format test report, "pretty" is good for human eyes
					'--glue', 'library_app.steps', // Where to look for your tests' steps
					'src/test/resources/library_app/features/', // Where to look for your features
					'--strict', // Causes cucumber to fail if any step definitions are still undefined
					'--tags', selectedTags // Only runs certain tagged scenarios of tags arg is given
			]
		}
	}
}

// Jacoco test coverage configuration
jacoco {
	toolVersion = "0.8.2"
}
// select what reports to generate and where to put them
jacocoTestReport {
	reports {
		xml.enabled true
		xml.destination file("${buildDir}/jacoco/test-report.xml")
		csv.enabled false
		html.destination file("${buildDir}/jacoco/html")
	}
}

// Sonarqube specific configuration
sonarqube {
	properties {
		property "sonar.projectKey", "team-600-server"
		property "sonar.projectName", "team 600 - server"
		property "sonar.sourceEncoding", "UTF-8"
		property "sonar.host.url", "https://csse-s302g6.canterbury.ac.nz/sonarqube/"
		property "sonar.login", "0a1467036e853397c101cf62ed6658536721f7e4"
	}
}

bootRun {
	bootRun.systemProperty 'spring.profiles.active', "${springProfile}"
}